//===== eAthena Script ======================================= 
//= Morroc Town
//===== By: ================================================== 
//= kobra_k88; L0ne_W0lf
//===== Current Version: ===================================== 
//= 2.0
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Morroc Town NPCs
//===== Additional Comments: ================================= 
//= Fully working
//= Fixed Lapidary sprite, Implemented ANTHELL trigger
//= (in both NPC/Warps) [Lupus]. Removed it from NPC and put in Warp
//= 1.3 Removed some NPC due to Assassin Job Quest [kobra_k88]
//= 1.3b mobs name corrected [Lupus], 1.3c - Adv classes support
//= 1.4 Fixed typos [Nexon]
//= 1.5 Added a lvl 4 weapon quest related NPC [MasterOfMuppets]
//= 1.6 Added Bartender NPC, made by Persian69 [Vicious_Pucca]
//= 1.7 Removed Duplicates [Silent]
//= 1.8 Rescripted to Aegis 10.3 Standards. [L0ne_W0lf]
//= 1.9 Removed duplicate NPC, changed temp char vars to temp npc. [L0ne_W0;f]
//= 2.0 Fixed bad NPC header data to comply with rev. 11603. [L0ne_W0lf]
//============================================================

// Morroc
//============================================================
morocc_mem,68,260,0	script	Towner#Spencer	99,{
	switch(rand(3)) {
	case 1:
	case 2:
		mes "[Spencer]";
		mes "You know...";
		mes "Those giant, triangular buildings at the NorthWest corner of Morroc known are known to us as Pyramids...";
		next;
		mes "[Spencer]";
		mes "Those things have been around here for thousands and thousands of years. No one knows when and why they were built, or who built them.";
		next;
		mes "[Spencer]";
		mes "All we know is that tons of monsters live inside those weird buildings. You might wanna stay away from those really dangerous places.";
		close;

	default:
		mes "[Spencer]";
		mes "Man...";
		next;
		mes "[Spencer]";
		mes "Oh...";
		mes "Oh man...";
		next;
		mes "[Spencer]";
		mes "Man, I'm freaking bored!";
		next;
		mes "[Spencer]";
		mes "Hey.";
		mes "Let's play...";
		mes "Rock, paper, scissors, yeah?";
		next;
		mes "[Spencer]";
		mes "I feel it's fair to warn you, though, that here I'm the official regional champion of this time honored game of intellectual strategy.";
		next;
		if (select("Alright~:What...?!") == 1) {
			mes "[Spencer]";
			mes "Alright...";
			mes "One, two...";
			mes "Three!";
			next;
			switch(select("Rock:Paper:Scissors")) {
			case 1:
				switch(rand(3)) {
				case 1:
					emotion e_rock;
					mes "["+strcharinfo(0)+"]";
					mes "Rock...!";
					next;
					mes "[Spencer]";
					mes "What the...?!";
					mes "I see great minds think alike! But then again, so do stupid minds...";
					close;
				case 2:
					emotion e_paper;
					mes "["+strcharinfo(0)+"]";
					mes "Rock...!";
					next;
					mes "[Spencer]";
					mes "Looks like I win!";
					mes "Heh heh~ Still got it.";
					close;
				default:
					emotion e_scissors;
					mes "["+strcharinfo(0)+"]";
					mes "Rock...!";
					next;
					mes "[Spencer]";
					mes "...What?!";
					mes "No way!";
					mes "How did I lose?!";
					next;
					mes "[Spencer]";
					mes "So...";
					mes "I see you're";
					mes "not just a";
					mes "mere rookie...";
					close;
				}
			case 2:
				switch(rand(3)) {
				case 1:
					emotion e_rock;
					mes "["+strcharinfo(0)+"]";
					mes "Paper...!";
					next;
					mes "[Spencer]";
					mes "It can't be...";
					mes "Impossible...";
					mes "I lost?!";
					next;
					mes "[Spencer]";
					mes "You're not able";
					mes "to read minds...";
					mes "Are you?";
					close;
				case 2:
					emotion e_paper;
					mes "["+strcharinfo(0)+"]";
					mes "Paper...!";
					next;
					mes "[Spencer]";
					mes "So...";
					mes "It seems we are";
					mes "evenly matched...";
					next;
					mes "[Spencer]";
					mes "You are truly a";
					mes "worthy opponent...";
					close;
				case 3:
					emotion e_scissors;
					mes "["+strcharinfo(0)+"]";
					mes "Paper...!";
					next;
					mes "[Spencer]";
					mes "Bwah Hah Hah!";
					mes "You lose!";
					next;
					mes "[Spencer]";
					mes "Still...";
					mes "That was too";
					mes "close for comfort...";
					next;
					mes "[Spencer]";
					mes "I...";
					mes "I almost chose Rock...";
					close;
				}
			case 3:
				switch(rand(3)) {
				case 1:
					emotion e_rock;
					mes "["+strcharinfo(0)+"]";
					mes "Scissors...!";
					next;
					mes "[Spencer]";
					mes "BAM...!";
					mes "Smashed you up!";
					mes "Who's your daddy?!";
					close;
				case 2:
					emotion e_paper;
					mes "["+strcharinfo(0)+"]";
					mes "Scissors...!";
					next;
					mes "[Spencer]";
					mes "It can't be...!";
					mes "I've spent seven years training to get a black belt in this game!";
					close;
				case 3:
					emotion e_scissors;
					mes "["+strcharinfo(0)+"]";
					mes "Scissors...!";
					next;
					mes "[Spencer]";
					mes "Uh oh...";
					mes "Looks like";
					mes "you've got the";
					mes "eye of the tiger.";
					next;
					mes "[Spencer]";
					mes "It may be a tie,";
					mes "But next time...";
					mes "I will crush you.";
					close;
				}
			}
		}
		mes "[Spencer]";
		mes "That's fine...";
		mes "I understand if you're a little intimidated to take on ^7A378BThe Master^000000.";
		close;
	}
}

morocc_mem,116,266,0	script	Towner#moc	85,{
	if (BaseLevel > 98) {
		mes "[Ogleson]";
		mes "Oh man...";
		next;
		mes "[Ogleson]";
		mes "That blueish glow around you! You're really, really strong! That's soooo cool! You must not be afraid of anything, not even Desert Wolves.";
		next;
		mes "[Ogleson]";
		mes "I'm so jealous!";
		mes "Everyone must like you!";
		close;
	}
	mes "[Ogleson]";
	mes "If you have to go through the desert, you have to look out for the Desert Wolves. Those things are so mean and vicious...";
	next;
	mes "[Ogleson]";
	mes "Those wolves are always traveling around in a pack. If they find that you're threatening one of them, you'll have to take all of them on.";
	next;
	mes "[Ogleson]";
	mes "I'm just so glad that, somehow, they keep those wild creatures out of the city. Otherwise, none of us would be able to sleep at night.";
	close;
}

morocc_mem,208,85,0	script	Towner#2moc	83,{
	mes "[Silas]";
	mes "I work in the trading business, so I always have to cross the hot, dry Morroc Desert on business.";
	next;
	mes "[Silas]";
	mes "One time, while I was in the middle of the desert, I was so thirsty that I caught a cactus. As you may know, you can find water inside of those things.";
	next;
	mes "[Silas]";
	mes "But before I was able to cut it, it slapped me! Then it shot me in the arse with all these needles. Man, did that smart!";
	next;
	mes "[Silas]";
	mes "The worst part of it all? I had to buy some Holy Water from a stingy Merchant. Man, I hate it when they know Overcharge!";
	next;
	mes "[Silas]";
	mes "Later, I learned that it wasn't a normal cactus I found, but the monster we call 'Muka.' So, try to bring water on your long trips, it might save you some pain.";
	close;
}

morocc_mem,150,50,0	script	Towner#3moc	99,{
	mes "[Lapidary]";
	mes "The undisputed queen of jewels has to be the Diamond. After looking at it just once, no one can deny its beauty.";
	next;
	mes "[Lapidary]";
	mes "Many factors determine its value, but the most important thing is its size.";
	next;
	mes "[Lapidary]";
	mes "It should also be a perfect crystal, not cracked or chipped, if you want to sell the Diamond for a lot of zeny.";
	close;
}

morocc_mem,44,180,0	script	Towner#4moc	89,{
	switch(rand(2))  {
	case 1:
		mes "[Trevor]";
		mes "Hey guy.";
		mes "What are you doing here all the way in Morroc? You know, this place is full of Thieves, Rogues and Assassins, right?";
		next;
		if (BaseClass == Job_Thief) {
			mes "[Trevor]";
			mes "Heh heh~";
			mes "I guess that means you fit right in! It's nice to be in your element, don't you agree?";
		}
		else {
			mes "[Trevor]";
			mes "Wait a second...";
			mes "Only people of bravery, or maybe even greed, would wander around here for fun. You must be an adventurer, right?";
		}
		next;
		mes "[Trevor]";
		mes "Anyway, there's this dagger that brings incredible luck to however possesses it. Luck... I'm use we can all use more of that. Supposedly, the Thief that used to own it has never been caught!";
		next;
		mes "[Trevor]";
		mes "I guess that could be a helpful tip to someone like you.";
		close;
	default:
		mes "[Trevor]";
		mes "There's some guy around here that's making these toootally funky hats. Yeah, they're pretty weird.";
		next;
		mes "[Trevor]";
		mes "Supposedly, he's studied science, and he's making these hats that you can wear to beat the heat on hot days. But the way they look is totally ridiculous.";
		next;
		mes "[Trevor]";
		mes "Actually, to tell the truth, some of them might actually be cute. But I saw someone leave him wearing a fish on his head! It was soooo weird!";
		close;
	}
}

morocc_mem,79,111,4	script	Picky Lady#oldmoc	66,{
	mes "[Syvia]";
	mes "The fields of Rune-Midgard are infested with wild and dangerous monsters that threaten the lives of traveling adventurers...";
	next;
	mes "[Syvia]";
	mes "But, you've got to admit a bunch of them are soooo cute!";
	next;
	mes "[Syvia]";
	mes "You know, like how Spore sticks out its tongue after you kill it, or how little Picky wears that tiny egg shell sometimes?";
	next;
	mes "[Syvia]";
	mes "And you certainly can't deny that ^F08080Poring^000000 is the cutest of them all!";
	next;
	switch(select("What about Angeling?:How about Ghostring?:Quit Conversation")) {
	case 1:
		mes "[Syvia]";
		mes "Ooh! Angeling is even cuter! It's just like Poring, except it has angel wings! Of course, I don't know if they can actually fly...";
		next;
		mes "[Syvia]";
		mes "Angelings are rarely seen, but can be found among large groups of Porings living in one of the fields south of Prontera.";
		next;
		mes "[Syvia]";
		mes "Angeling is a high level monster with Holy property, so it's immune to most magic, aside from spells that have Neutral or Shadow attack properties.";
		next;
		mes "[Syvia]";
		mes "Hehe~ Don't you think I know a lot about Porings? I love them soooo much~";
		close;
	case 2:
		mes "[Syvia]";
		mes "Ghostring?!";
		mes "Yeah, that";
		mes "little guy";
		mes "is kind of... ";
		mes "gnarly looking.";
		next;
		mes "[Syvia]";
		mes "Ghostring is an evil ghost Poring. It's rarely seen, but can be found among mass groups of Porings living in one of the fields south of Prontera.";
		next;
		mes "[Syvia]";
		mes "Ghostring is a high-leveled monster with the Ghost property, so it can withstand all physical attacks.";
		next;
		mes "[Syvia]";
		mes "Damage can only be caused to Ghostring through magic spells or weapons with an a specific property.";
		next;
		mes "[Syvia]";
		mes "Hehe~ Don't you think I know a lot about Porings? I love them soooo much~";
		close;
	case 3:
		mes "[Syvia]";
		mes "Huh?! Where are you going? I was just about to tell you how cute Porings are! W-Wait~!";
		close;
	}
}

morocc_mem,76,75,4	script	Ant Man#oldmoc	47,{
	mes "[Akira]";
	mes "When you walk through the desert about one map North and 3 maps East, you'll find the entrance to Ant Hell.";
	next;
	switch(select("Ant Hell?:Ants?:End Conversation")) {
	case 1:
		mes "[Akira]";
		mes "Ant Hell seems to be a colony of giant ant monsters. If you wander in there, look out. They can be pretty dangerous.";
		next;
		mes "[Akira]";
		mes "Giearth, the elderly fairy, also roams around Ant Hill mining for Ores. If you bother him, you'll be in trouble, so watch out for him too.";
		next;
		mes "[Akira]";
		mes "To get to Ant Hell, you can head east till you see the bridge which connects to the Payon Forest. In the map right before that bridge, head north to find the entrance to Ant Hell.";
		close;
	case 2:
		mes "[Akira]";
		mes "Ant Hell is swarming with Ant monsters. You'll notice that they all classed with different names.";
		next;
		mes "[Akira]";
		mes "Andre is the weakest, Piere is comparatively average, and Deniro is the strongest Ant. Keep that in mind so you can fight the Ants suitable to your level.";
		next;
		mes "[Akira]";
		mes "Oh, and you should be especially careful not to drop items. Those ants will grab anything on the ground in a flash!";
		close;
	case 3:
		close;
	}
}

morocc_mem,123,58,4	script	Assassin Boy#oldmoc	118,{
	mes "[Timmy]";
	mes "Unbeatable in man-to-man fights, Assassins always overcome their enemies! Erm, always overcome their enemies without a trace.";
	next;
	switch(select("Say what?:Where I can find the Assassin clan?:End Conversation")) {
	case 1:
		mes "[Timmy]";
		mes "I hear Assassins practice killing people, quietly and stealthfully of course, everyday! I want to become one so bad!";
		next;
		mes "[Timmy]";
		mes "But...";
		mes "I left home three months ago, and have been searching for their secret base ever since. Are they really that invisible?!";
		close;
	case 2:
		mes "[Timmy]";
		mes "Well, I hear that if you leave this town and go 2 maps east, and then 2 maps South, you should be able to find it.";
		next;
		mes "[Timmy]";
		mes "The 'Mirage Tower,' the head building of Sograt Desert, is supposed to appear in this awesome sandstorm! But, I still haven't found it.";
		next;
		mes "[Timmy]";
		mes "If you find them, and the clan master thinks you're qualified, you can become an Assassin! Or, at least, I think...";
		next;
		mes "[Timmy]";
		mes "Oh..!";
		mes "And I hear the coolest Assassin of them all is the Assassin Cross! But, that's even a bigger mystery~";
		close;
	case 3:
		mes "[Timmy]";
		mes "Assassin is one of the advanced jobs for Thief, and specializes in fighting with Neutral and Poison property attacks.";
		next;
		mes "[Timmy]";
		mes "They're also very sneaky! I hear that they're able to cloak so that no one can see them!";
		close;
	}
}

morocc_mem,180,155,4	script	Dimitri#oldmoc	49,{
	mes "[Dimitri]";
	mes "Man, this place is so hot! You would think they would sell Deodorant, maybe even some cologne in this city, but noooooo...";
	next;
	switch(select("About the desert sands:About the remedy for Fatigue:End Conversation")) {
	case 1:
		mes "[Dimitri]";
		mes "The desert?!";
		mes "It's definitely";
		mes "much hotter than";
		mes "this city.";
		next;
		mes "[Dimitri]";
		mes "Still, you can sit down and take a rest whenever you need to and your ass won't burn. I guess the Morroc sand doesn't conduct heat as much as it should.";
		next;
		mes "[Dimitri]";
		mes "I mean, when I sit down, it does feel ^EE2C2Cpleasantly toasty^000000. I get almost a guilty sensation when I do that.";
		next;
		mes "[Dimitri]";
		mes "Try it when you're in the desert sometime. You'll see that you can still recover your HP and SP.";
		close;
	case 2:
		mes "[Dimitri]";
		mes "When you're tired, just drink a Potion. Those things taste great!";
		next;
		mes "[Dimitri]";
		mes "I know, the Pharmacist who can make them always says ^EE3B3BDon't drink too much, it's abuse, blah blah blah^000000, but whatever.";
		next;
		mes "[Dimitri]";
		mes "Red Potions have become steadily popular since they're so affordable, even though they only recover a little bit of HP.";
		next;
		mes "[Dimitri]";
		mes "So...";
		mes "Just know that you should try to carry Potions in case you run into some sort of crazy situation, alright?";
		close;
	case 3:
		mes "[Dimitri]";
		mes "Sometimes 'Milk' comes out of 'PecoPeco's Egg.' Now..";
		mes "I'm not sure how the cow's milk comes out of some bird's egg...";
		next;
		mes "[Dimitri]";
		mes "I may not be a man of science, but how is that even possible?.";
		mes "I mean, did the Milk come prepackaged with the egg, or did it get in there somehow?..";
		mes "Oh, whatever.. It's just not the time for this.";
		close;
	}
}

morocc_mem,234,273,4	script	Fly Man#oldmoc	54,{
	mes "[Armani]";
	mes "I really saw it...";
	mes "The Fly Lord!";
	mes "Why doesn't anyone";
	mes "believe me?!";
	next;
	if (select("The Fly Lord?!:Isn't that a book?") == 1) {
		mes "[Armani]";
		mes "Yes, yes! I'm talking about the Dragon Fly, master of all flies!";
		next;
		mes "[Armani]";
		mes "It's much stronger than the other flies, and when you kill it, there's even a chance that you might earn a ^880000Clip^000000 item!";
		next;
		mes "[Armani]";
		mes "You can insert certain monster cards into the Clip item. Those Clip accessories seem pretty valuable to you adventurers...";
		next;
		mes "[Armani]";
		mes "Anyhow, why don't you go search for it? I'm sure if you're lucky, you'll find a Dragon Fly around here!";
		close;
	}
	mes "[Armani]";
	mes "Yes...";
	mes "Yes, it is.";
	next;
	mes "[Armani]";
	mes "It's a pretty good novel, even though 'The Fly Lord' isn't really the exact title. It's actually Lord of the--Wait, why are we talking about this?";
	close;
}

morocc_mem,277,213,4	script	Uncle Morroc#oldmoc	48,{
	mes "[Phlanette]";
	mes "Morroc is located in an extremely dry region, surrounded by desert. No place in the world is as hot as Morroc.";
	next;
	if (select("Tell me about the desert.:Quit Conversation") == 1) {
		mes "[Phlanette]";
		mes "You want to";
		mes "know about";
		mes "the desert?";
		mes "Well, um...";
		next;
		mes "[Phlanette]";
		mes "Due to low annual rainfall, low humidity and high evaporation rate, little vegetation can be found in the desert.";
		next;
		mes "[Phlanette]";
		mes "Nonetheless, there are a few hardy plants that can survive and grow in the desert. I learned all that in the Juno Library.";
		next;
		mes "[Phlanette]";
		mes "Unfortunately some of those plants growing around Morroc have been mutated into monsters. One of those is Muka, the mutated cactus.";
		next;
		mes "[Phlanette]";
		mes "Although it can be pretty uncomfortable for us, other creatures can thrive in the desert near Morroc.";
		close;
	}
	close;
}

// Inside Morroc
//============================================================
oldmoc_in,112,122,0	script	Grampa#oldmoc	61,{
	mes "[Old Scholar]";
	mes "I've devoted my life to researching the mysterious pyramids near Morroc. I haven't been able to concretely confirm anything yet, but...";
	next;
	mes "[Old Scholar]";
	mes "I'm sure that the largest pyramid contains the tomb of the ancient king, Osiris! I'm willing to stake my life on it!";
	next;
	mes "[Old Scholar]";
	mes "According to my notes, Osiris, one of the greatest kings in history, sleeps eternally within the depths of that pyramid. If only I were younger, I could see it for myself...";
	next;
	if (countitem(1030) > 0) {
		mes "[Old Scholar]";
		mes "Wait...";
		mes "Do I smell...?";
		mes "It is! That's the scent of ^993300Tiger's Footskin^000000!";
		next;
		mes "[Old Scholar]";
		mes "One glance at it can restore your strength, one sniff and your blood boils with vigorous pleasure...";
		next;
		mes "[Old Scholar]";
		mes "Just one bite... Will revive my virility!!!! The Tiger's Footskin~!! Ohhhh! Ohh My God !! Please!!! Please give me that... Please...";
		next;
		mes "[Old Scholar]";
		mes "It's just...";
		mes "If you gave it to me, I would give you absolutely nothing in return. ^EE0000Nothing at all^000000...";
		close;
	}
	mes "[Old Scholar]";
	mes "I guess it'll be up to the younger generation to explore the mysteries of the past firsthand...";
	close;
}


// Morroc Field
//============================================================
moc_fild16,199,212,4	script	Uncle Assassin#oldmoc	55,{
	mes "[Hashisid]";
	mes "Assassins are trained to approach their enemies steathily, as well as to shroad their intent. For this purpose, we never look our targets in the eye.";
	next;
	if (select("Notion of Assassin:Quit Conversation") == 1) {
		mes "[Hashisid]";
		mes "Assassins may be commonly known as infiltrators and murderers who kill without remorse.";
		next;
		mes "[Hashisid]";
		mes "In actuality, the Assassin clan is forbidden to harm innocent people, or at least, not without good reason.";
		next;
		mes "[Hashisid]";
		mes "Our true directive is to assassinate evil creatures, and to use our stealth to gather intelligence for the good of all Rune-Midgard.";
		close;
	}
	mes "[Hashisid]";
	mes "Once upon a time, our ancestors would smoke tobacco before performing their duties.";
	next;
	mes "[Hashisid]";
	mes "However, we no longer do so, since insect or animal monsters are very senstive to the smell.";
	next;
	mes "[Hashisid]";
	mes "There are many factors that contribute to successful stealth. Depending on your opponent, it's not enough to just be invisible sometimes...";
	close;
}

moc_fild16,177,246,4	script	Assassin#oldmoc	118,{
	mes "[La Conte]";
	mes "Once Thiefs are promoted to Assassins, they will be able to vary their battle style.";
	next;
	mes "[La Conte]";
	mes "There are 2 main battle styles available to Assassins. They may either wield dual daggers, or fight with a set of Katars.";
	next;
	switch(select("Specialty of Katar:Specialty of Dual Daggers:Quit Conversation")) {
	case 1:
		mes "[La Conte]";
		mes "A set of Katars is worn on both of the hands, and allows Assassins to quickly slash their enemies.";
		next;
		mes "[La Conte]";
		mes "Anyone can actually buy a basic set of Katars in Morroc somewhere. Of course, only Assassins can use them.";
		next;
		mes "[La Conte]";
		mes "The right Katar usually does more damage, while the left Katar is used for the follow-through.";
		next;
		mes "[La Conte]";
		mes "But since Katars are equipped on both hands, you can't equip a shield or an extra weapon.";
		next;
		mes "[La Conte]";
		mes "Compared to Dual Daggers, Katars have faster attack speed. Also, the ^663399Sonic Blow^000000 skill can only be used with Katars.";
		next;
		mes "[La Conte]";
		mes "You can learn the Sonic Blow skill at ^663399Level 4 Katar Mastery^000000. If you're an Assassin, it's a handy skill to know.";
		close;
	case 2:
		mes "[La Conte]";
		mes "Dual Daggers enables you to equip 2 different kinds of Daggers at the same time.";
		next;
		mes "[La Conte]";
		mes "Of course, there are other weapons you can equip aside from daggers, but they'll probably be lacking in attack speed.";
		next;
		mes "[La Conte]";
		mes "Also, without a dagger in your right hand, you won't be able to use the ^663399Double Attack^000000 skill.";
		next;
		mes "[La Conte]";
		mes "So with Dual Daggers, you would have a double attack with the right hand dagger, and a single attack with your left hand weapon.";
		next;
		mes "[La Conte]";
		mes "So that's three strikes in one blow! You can't argue against that kind of damage!";
		close;
	case 3:
		mes "[La Conte]";
		mes "Hopefully you will make good use of the weapons at your disposal. Remember the importance of strategy and planning your attacks.";
		close;
	}
}

// Other NPCs
//============================================================
//Bartender
oldmoc_in,166,76,3	script	Bartender#old02	46,{
	mes "[Bartender]";
	mes "What are you going to order?";
	next;
	menu "Tropical Sograt",L1,"Vemillion the Beach",L2,"Nothing.",-;

	mes "[Bartender]";
	mes "Hmm...";
	close;

L1:
	if (Zeny < 1000 ) goto Nomoney;
	getitem 12112,1;
	set Zeny,Zeny-1000;
	mes "[Bartender]";
	mes "Here you go.";
	mes "Fruits are major ingredients,";
	mes "but don't drink too much.";
	close;

L2:
	if (Zeny < 1000 ) goto Nomoney;
	getitem 12113,1;
	set Zeny,Zeny-1000;
	mes "[Bartender]";
	mes "Here you go.";
	mes "but don't drink too much.";
	close;

Nomoney:
	mes "[Bartender]";
	mes "Are you asking me to give it for free?";
	mes "You are one crazy person.";
	mes "Don't even think about drinking if you don't have 1,000 zeny.";
	close;
}

// Lvl 4 weapon quest related NPC ================================

morocc_mem,289,230,3	script	Citizen#old06	92,{
	mes "[Citizen]";
	mes "Meeting a dead man is basically";
	mes "impossible.";
	mes "Even if you met one,";
	mes "he would not have the full memory of his life.";
	next;
	mes "[Citizen]";
	mes "But if you brought a thing that he";
	mes "used to keep in his life,";
	mes "it would be possible to retrieve his memory of the life.";
	mes "Of course, we can confirm this theory only";
	mes "when we meet a dead man.";
	close;
}

// Morocc
//============================================================
morocc_mem,156,97,4	script	Kafra Employee::kaf_oldmorocc	4_F_KAFRA5,{
	cutin "kafra_05",2;
	callfunc "F_KafSet";
	mes "[Kafra Employee]";
	mes "The Kafra Corporation";
	mes "is always working to provide";
	mes "you with convenient services.";
	mes "How may I be of assistance?";
	callfunc "F_Kafra",5,0,0,60,930;
	savepoint "morocc_mem",156,46;
	callfunc "F_KafEnd",0,1,"in the city of Morroc";
}

morocc_mem,160,258,4	script	Kafra::kaf_oldmorocc2	4_F_KAFRA4,{
	cutin "kafra_04",2;
	callfunc "F_KafSet";
	mes "[Kafra Employee]";
	mes "Welcome!";
	mes "The Kafra Corporation";
	mes "will always support the";
	mes "adventurers of Rune-Midgard";
	mes "with its excellent service. So";
	mes "what can I do for you today?";
	callfunc "F_Kafra",5,0,0,60,930;
	savepoint "morocc_mem",157,272;
	callfunc "F_KafEnd",0,1,"in the city of Morroc";
}

//
//===== eAthena Script =======================================
//= Morroc Warp Script
//===== By: ==================================================
//= Athena (1.0)
//= Nana (1.2)
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= Any Athena Version
//===== Description: =========================================
//= Warp Points for Morroc and Assassin Guild
//===== Additional Comments: =================================
//= No Comment
//============================================================

//= Morroc City ==============================================
morocc_mem,160,17,0	warp	mocold001	2,2,moc_fild12,159,378
morocc_mem,160,183,0	warp	mocold002	2,1,moc_castle,94,181
morocc_mem,160,297,0	warp	mocold003	2,2,moc_fild07,198,25
morocc_mem,197,66,0	warp	mocold01	1,1,morocc_in,83,92
morocc_mem,22,294,0	warp	mocold02	2,5,moc_ruins,156,42
morocc_mem,24,164,0	warp	mocold004	1,2,moc_fild19,164,107
morocc_mem,253,56,0	warp	mocold03	2,1,morocc_in,134,74
morocc_mem,26,297,0	warp	mocold02-1	5,2,moc_ruins,156,42
morocc_mem,274,269,0	warp	mocold04	2,2,morocc_in,138,136
morocc_mem,283,170,0	warp	mocold07	2,2,morocc_in,108,176
morocc_mem,302,207,0	warp	mocold005	2,2,moc_fild10,22,207
morocc_mem,46,46,0	warp	mocold05	2,1,morocc_in,68,72
morocc_mem,52,259,0	warp	mocold06	1,2,morocc_in,180,65
morocc_mem,85,55,0	warp	mocold08	2,2,morocc_in,44,149
morocc_mem,98,68,0	warp	mocold09	1,1,morocc_in,44,175
morocc_in,108,179,0	warp	mocold07-1	2,2,morocc_mem,283,173
morocc_in,134,77,0	warp	mocold03-1	3,1,morocc_mem,251,59
morocc_in,136,136,0	warp	mocold04-1	1,4,morocc_mem,271,269
morocc_in,183,65,0	warp	mocold06-1	2,3,morocc_mem,55,259
morocc_in,44,146,0	warp	mocold08-1	3,2,morocc_mem,82,52
morocc_in,44,178,0	warp	mocold09-1	2,2,morocc_mem,100,70
morocc_in,68,75,0	warp	mocold05-1	2,1,morocc_mem,49,49
morocc_in,83,90,0	warp	mocold01-1	4,1,morocc_mem,199,64

//= Morroc Ruins ============================================
moc_ruins,161,40,0	warp	mocold02-2	2,4,morocc_mem,26,293
moc_ruins,157,37,0	warp	mocold02-3	5,2,morocc_mem,26,293
moc_ruins,54,161,0	warp	mocold006	2,3,moc_pryd01,192,9
moc_ruins,71,16,0	warp	mocold007	8,2,moc_fild19,71,167

//= Morroc Castle ===========================================
moc_castle,94,183,0	warp	moccold001	2,1,morocc_mem,160,185
moc_castle,107,163,0	warp	moccold01	2,3,moc_castle,124,163
moc_castle,120,163,0	warp	moccold01-1	2,3,moc_castle,103,163
moc_castle,120,75,0	warp	moccold02	2,3,moc_castle,56,33
moc_castle,59,34,0	warp	moccold02-1	2,3,moc_castle,124,75
moc_castle,134,101,0	warp	moccold03	1,1,moc_castle,134,128
moc_castle,134,124,0	warp	moccold-03	2,2,moc_castle,134,98
moc_castle,134,139,0	warp	moccold04	1,1,moc_castle,134,160
moc_castle,134,156,0	warp	moccold04-1	2,2,moc_castle,134,136
moc_castle,149,163,0	warp	moccold05	2,3,moc_castle,162,163
moc_castle,158,163,0	warp	moccold05-1	2,3,moc_castle,145,163
moc_castle,16,131,0	warp	moccold06	1,2,moc_castle,16,164
moc_castle,16,160,0	warp	moccold06-1	1,2,moc_castle,16,125
moc_castle,170,131,0	warp	moccold07	2,2,moc_castle,170,163
moc_castle,170,160,0	warp	moccold07-1	1,1,moc_castle,170,128
moc_castle,29,163,0	warp	moccold08	2,2,moc_castle,44,163
moc_castle,40,163,0	warp	moccold08-1	2,2,moc_castle,25,163
moc_castle,51,114,0	warp	moccold09	3,2,moc_castle,54,65
moc_castle,54,69,0	warp	moccold09-1	3,2,moc_castle,52,117
moc_castle,54,139,0	warp	moccold10	2,2,moc_castle,54,160
moc_castle,54,156,0	warp	moccold10-1	2,2,moc_castle,54,134
moc_castle,63,89,0	warp	moccold11	2,3,moc_castle,83,89
moc_castle,80,89,0	warp	moccold11-1	2,3,moc_castle,60,89
moc_castle,69,163,0	warp	moccold12	2,2,moc_castle,86,163
moc_castle,82,163,0	warp	moccold12-1	2,2,moc_castle,66,163
moc_castle,88,93,0	warp	moccold13	1,1,moc_castle,94,119
moc_castle,94,116,0	warp	moccold13-1	1,1,moc_castle,88,90
moc_castle,96,90,0	warp	moccold13-2	1,1,moc_castle,94,119
moc_castle,92,67,0	warp	moccold14	3,2,moc_castle,92,85
moc_castle,92,82,0	warp	moccold14-1	3,2,moc_castle,92,63
moc_castle,94,143,0	warp	moccold15	3,2,moc_castle,94,160
moc_castle,94,156,0	warp	moccold15-1	3,2,moc_castle,94,140

//=======================================================
// Morroc
//=======================================================
oldmoc_in,141,67,0	shop	Weapon Dealer#oldmoc	58,1750:-1,1751:-1,1701:-1,1601:-1,1201:-1,1204:-1,1207:-1,1210:-1,1213:-1,1216:-1,1219:-1,1222:-1,1250:-1,1252:-1,1254:-1
oldmoc_in,141,60,0	shop	Armor Dealer#oldmoc	58,2101:-1,2103:-1,2401:-1,2403:-1,2405:-1,2501:-1,2503:-1,2218:-1,2301:-1,2303:-1,2305:-1,2321:-1,2328:-1,2332:-1,2307:-1,2309:-1,2335:-1,2628:-1
morocc_mem,35,69,6	shop	Trader#oldmoc	89,2609:-1,1516:-1,1522:-1
morocc_mem,139,92,6	shop	Trader#oldmoc2	99,513:-1,513:-1,513:-1,513:-1,513:-1,513:-1
morocc_mem,145,44,6	shop	Jeweler#oldmoc	99,730:-1,2613:-1
morocc_mem,147,102,4	shop	Tool Dealer#oldmoc	93,1750:-1,611:-1,501:-1,502:-1,503:-1,504:-1,506:-1,645:-1,656:-1,601:-1,602:-1,1065:-1
morocc_mem,151,243,6	shop	Tool Dealer#oldmoc2	99,1750:-1,611:-1,501:-1,502:-1,503:-1,504:-1,506:-1,645:-1,656:-1,601:-1,602:-1,1065:-1,2242:-1
morocc_mem,157,72,6	shop	Butcher#oldmoc	85,517:-1
morocc_mem,166,51,2	shop	Jeweler#oldmoc2	102,721:-1,723:-1,726:-1,728:-1,729:-1
morocc_mem,170,101,2	shop	Item Collector#oldmoc	85,911:-1,528:-1,919:-1,925:-1
morocc_mem,206,250,0	shop	Item Collector#oldmoc2	85,911:-1,528:-1,919:-1,925:-1
morocc_mem,244,134,5	shop	Trader#oldmoc3	93,2612:-1
morocc_mem,259,193,4	shop	Trader#oldmoc4	99,747:-1
morocc_mem,268,193,0	shop	Trader#oldmoc5	93,748:-1
morocc_mem,269,167,4	shop	Pet Groomer#oldmoc	125,537:-1,643:-1,10013:-1,10014:-1
oldmoc_in,132,57,0	shop	Weapon Dealer#oldmoc2	99,1146:-1,1245:-1
in_moc_16,22,20,7	shop	Sepulchral Merchant#omoc	880,1771:-1

morocc,43,108,5	script	Sharp-Looking Kid#dan_07	4_KID01,{
	if (BaseClass == Job_Assassin) {
		mes "[Jack]";
		mes "In spite of the chaotic mess in their town, ";
		mes "there are still some of those who pay visits here.";
		mes "Maybe the stronghold itself is a real one of a kind..";
		close;
	}
	else {
		if (countitem(7416) > 0) {
			mes "[Jack]";
			mes "A letter of recommendation?";
			mes "Oh, boy. Looks like you've brought me a very, very old relic from the past.";
			mes "Well, guess you should go in and see the master.";
			close;
		}
		else {
			mes "[Jack]";
			mes "Hey, what are you peeking at?";
			mes "Don't you have better things to do? You'd better go to the shelter and save your ass.";
			close;
		}
	}
}
morocc_mem,43,108,5	duplicate(Sharp-Looking Kid#dan_07)	Sharp-Looking Kid#old	4_KID01

morocc,45,110,0	script	que_job01#01	WARPNPC,2,2,{
OnTouch:
	if (BaseClass == Job_Assassin) {
		warp "que_job01",9,94;
		end;
	}
	else {
		mes "[Jack]";
		mes "Wait, that's the restricted area....";
		mes "......";
		mes "Oh, well.";
		next;
		mes "[Jack]";
		mes "Things have changed.";
		mes "Just do whatever you want!";
		close2;
		warp "que_job01",9,94;
		end;
	}
}
morocc_mem,45,110,0	duplicate(que_job01#01)	que_job01#old01	WARPNPC

que_job01,6,94,0	warp	morocc#01	2,2,morocc_mem,45,103	// Changed to old morocc
que_job01,17,48,0	warp	que_job01#02	2,2,que_job01,68,92
que_job01,68,96,0	warp	que_job01#03	2,2,que_job01,17,53

que_job01,80,77,0	script	que_job01#04	WARPNPC,2,2,{
OnTouch:
	if (BaseClass == Job_Assassin) {
		warp "que_job01",61,50;
		end;
	}
	else if (mao_request > 0) {
		warp "que_job01",61,50;
		end;
	}
	else {
		mes "[Litheron]";
		mes "Whoa, you're not";
		mes "allowed to be in here.";
		mes "Hey, master! Do you";
		mes "know this guy?";
		next;
		if (prt_curse == 24) {
			mes "[" + strcharinfo(0) + "]";
			mes "I don't care what you say, I'm coming in!!!";
			next;
			mes "[Litheron]";
			mes "Hey, master-! What do we do with this guy?";
			mes "He sure seems tough!";
			next;
			mes "[Master]";
			mes "Hmm. Let him do what he wants to do.";
			mes "Passing through one door doesn't make any difference.";
			next;
			mes "[Litheron]";
			mes "Hah! Did you hear that?";
			mes "Alright, how about this?";
			mes "I'll let you in if you buy me a drink.";
			mes "Besides, you can't risk making trouble here: this place";
			mes "is full of deadly Assassins.";
			next;
			switch(select("Sure:Why should I?!")) {
			case 1:
				if (Zeny > 999) {
					mes "[Litheron]";
					mes "Heh, that's what";
					mes "I'm talking about!";
					mes "Hey, bartender! Gimme";
					mes "the usual! I like your";
					mes "style, adventurer...";
					Zeny -= 1000;
					next;
					mes "[Litheron]";
					mes "Alright, you can come";
					mes "on in. But don't you dare";
					mes "breathe a word about this";
					mes "bar to another living soul.";
					close2;
					warp "que_job01",61,50;
					end;
				}
				else {
					mes "[Litheron]";
					mes "Huh...";
					mes "Oh, you don't even";
					mes "have enough zeny to";
					mes "buy water here. Oh boy...";
					mes "If you really want to enter,";
					mes "make sure you have the cash!";
					close2;
					warp "que_job01",85,77;
					end;
				}
			case 2:
				mes "[Litheron]";
				mes "Not the saavy type,";
				mes "are you...? Fine, fine.";
				mes "If you're not gonna do";
				mes "me any favors, then why";
				mes "should I help you? Go away!";
				close2;
				warp "que_job01",85,77;
				end;
			}
		}
		else {
			mes "[Master]";
			mes "Not at all.";
			mes "Make sure that this";
			mes "one isn't allowed";
			mes "to enter.";
			next;
			mes "[Litheron]";
			mes "You heard the man.";
			mes "You better retreat";
			mes "from this place while";
			mes "you still have legs...";
			close2;
			warp "que_job01",85,77;
			end;
		}
	}
}

que_job01,65,50,0	warp	que_job01#05	2,2,que_job01,84,77

que_job01,81,79,5	script	Idle Knight#dan_08	4_M_JOB_KNIGHT1,{
	mes "[Litheron]";
	mes "What's the matter?";
	mes "Oh, I see... You're";
	mes "probably wondering why";
	mes "a Knight like me is in a";
	mes "secret Assassin's pub.";
	if (BaseClass == Job_Knight) {
		mes "What about you, huh?";
	}
	next;
	mes "[Litheron]";
	mes "You know.. I just came to take a short breather. This whole restoration work is killing me.";
	mes "It's almost impossible to spot a place for a little drink these days, if you know what I mean.";
	close;
}

que_job01,82,95,3	script	Bar Master#moc_main01	1_ETC_01,{
	if (checkweight(908,200) == 0) {
		mes "^3355FFWait a second!";
		mes "Right now, you're carrying";
		mes "too many things with you.";
		mes "Please come back after";
		mes "using the Kafra Service";
		mes "to store some of your items.^000000";
		close;
	}
	if (BaseClass == Job_Assassin) {
		mes "[Master]";
		mes "Please, come in.";
		mes "Care for a drink?";
	}
	else if ((BaseJob == Job_Thief) && (BaseClass != Job_Assassin)) {
		mes "[Master]";
		mes "......Huh. How did a ruffian like you get in here?";
		mes "Well, I guess Jack's not doing his job.";
		next;
		mes "[" + strcharinfo(0) + "]";
		mes "Hey,master, don't be so harsh. We know each other, don't we?";
		mes "You don't wanna hurt your old friend's feeling~";
		next;
		mes "[Master]";
		mes "Whatever you say.";
		mes "So,what do you wanna drink?";
	}
	else {
		mes "[Master]";
		mes "....Hmm...";
		mes "What do you wanna drink?";
	}
	next;
	switch(select("Order a Drink:Ask About Mission:Cancel")) {
	case 1:
		mes "[Master]";
		mes "In this place, I only serve two drinks.";
		mes "You want a Tropical Sograt or";
		mes "a Vermilion on the Beach?";
		next;
		switch(select("Tropical Sograt:Vermilion on the Beach:Do you offer any free drinks?")) {
		case 1:
			if (checkweight(908,200) == 0) {
				mes "[Master]";
				mes "Hey, why did you bring";
				mes "so much stuff with you?";
				mes "I can't give you anything";
				mes "to drink if you don't have";
				mes "the room to carry it around.";
				mes "Clear out your inventory!";
				close;
			}
			else {
				if (BaseClass == Job_Assassin) {
					if (Zeny < 800) {
						mes "[Master]";
						mes "You know...";
						mes "I was going to charge you";
						mes "800 zeny for this drink, but";
						mes "it looks like you can't afford";
						mes "it right now. Come back when";
						mes "you have the zeny, alright?";
					}
					else {
						mes "[Master]";
						mes "Most bartenders would";
						mes "usually charge 1,000 zeny";
						mes "for this stuff, but I'll only";
						mes "ask you for 800. Enjoy it,";
						mes "my friend, and drink it as";
						mes "deeply as you would life.";
						Zeny -= 800;
						getitem 12112,1; //Tropical_Sograt
					}
					close;
				}
				else {
					if (Zeny < 1000) {
						mes "[Master]";
						mes "Hey, you don't have";
						mes "the zeny for this drink.";
						mes "Make sure you come back";
						mes "with 1,000 zeny if you want";
						mes "me to fix you something, okay?";
						close;
					}
					else {
						mes "[Master]";
						mes "For Assassins, I charge";
						mes "800 zeny, but for you, I'm";
						mes "gonna charge 1,000. No";
						mes "hard feelings, but you're";
						mes "already lucky to be here.";
						mes "Hey, enjoy your drink~";
						Zeny -= 1000;
						getitem 12112,1; //Tropical_Sograt
						close;
					}
				}
			}
		case 2:
			if (checkweight(908,200) == 0) {
				mes "[Master]";
				mes "Hey, why did you bring";
				mes "so much stuff with you?";
				mes "I can't give you anything";
				mes "to drink if you don't have";
				mes "the room to carry it around.";
				mes "Clear out your inventory!";
				close;
			}
			else {
				if (BaseClass == Job_Assassin) {
					if (Zeny < 800) {
						mes "[Master]";
						mes "You know...";
						mes "I was going to charge you";
						mes "800 zeny for this drink, but";
						mes "it looks like you can't afford";
						mes "it right now. Come back when";
						mes "you have the zeny, alright?";
					}
					else {
						mes "[Master]";
						mes "Most bartenders would";
						mes "usually charge 1,000 zeny";
						mes "for this stuff, but I'll only";
						mes "ask you for 800. Enjoy it,";
						mes "my friend, and drink it as";
						mes "deeply as you would life.";
						Zeny -= 800;
						getitem 12113,1; //Vermilion_The_Beach
					}
					close;
				}
				else {
					if (Zeny < 1000) {
						mes "[Master]";
						mes "Hey, you don't have";
						mes "the zeny for this drink.";
						mes "Make sure you come back";
						mes "with 1,000 zeny if you want";
						mes "me to fix you something, okay?";
						close;
					}
					else {
						mes "[Master]";
						mes "For Assassins, I charge";
						mes "800 zeny, but for you, I'm";
						mes "gonna charge 1,000. No";
						mes "hard feelings, but you're";
						mes "already lucky to be here.";
						mes "Hey, enjoy your drink~";
						Zeny -= 1000;
						getitem 12113,1; //Vermilion_The_Beach
						close;
					}
				}
			}
		case 3:
			mes "[Master]";
			mes "What are you...?";
			mes "I just told you";
			mes "I only serve two";
			mes "drinks. If you want";
			mes "water, you oughtta";
			mes "go someplace else.";
			close;
		}
	case 2:
		if (mao_request == 1) {
			if (BaseClass == Job_Assassin) {
				mes "[Master]";
				mes "A mission...";
				mes "Don't you think you're little too late?";
				mes "All the requests were cancelled and there's nothing left for you to do.";
				mes "Besides, first things first. The restoration work for the city is the most urgent.";
				close;
			}
			else {
				mes "[Master]";
				mes "Huh?";
				next;
				select("Show him the Letter of Recommendation.");
				if (countitem(7416) > 0) {
					mes "[Master]";
					mes "Ah.. I think you're little too late.";
					mes "I've got nothing to say if this's about the request sent to each guild.";
					mes "I'm sorry you've come this far for nothing.";
					close;
				}
				else {
					mes "[Master]";
					mes "What are you talking about?";
					next;
					mes "[Master]";
					mes "................You're looking way too suspicious now.";
					mes " ";
					mes "Boys, show this guy the door!!";
					next;
					mes "[Drinking Boys]";
					mes "- Yes, Sir!";
					next;
					mes "- You've got kicked out of the place. -";
					close2;
					warp "morocc_mem",45,106;	// Changed to Old morocc
					end;
				}
			}
		}
		else {
			if (BaseClass == Job_Assassin) {
				mes "[Master]";
				mes "Let me take a guess. You haven't been assigned any missions from this joint, eh?";
				mes "By the way, you like this place?";
				next;
				mes "[Master]";
				mes "You know, I kinda...";
				mes "happen to really love this little pub of mine..";
				mes "where some secret businesses take place..";
				next;
				mes "[Master]";
				mes "If you want, I'll recommend";
				mes "you to the guildmaster for";
				mes "a mission. Until then, you've";
				mes "got to focus on your training.";
				mes "And until you train... Why";
				mes "don't you enjoy a drink?";
				close;
			}
			else {
				mes "[Master]";
				mes "Mission...?";
				mes "Come on, what are";
				mes "you talking about?";
				mes "Stop talking crazy and";
				mes "order something to drink.";
				close;
			}
		}
	case 3:
		mes "[Master]";
		mes "Hmmm.";
		close;
	}
}

que_job01,49,49,5	script	Tao#dan_09	4_F_YUNYANG,{
	if (BaseClass == Job_Assassin) {
		mes "[Tao]";
		mes "Welcome, meow~";
		mes "How's out, meow?";
		next;
		if (prt_curse == 24) {
			switch(select("Is Marjana in?:I'm here to see you,Tao.")) {
			case 1:
				mes "[Tao]";
				mes "She's in the room on your left, meow~";
				mes "But then, how d'you know she's here, meow?";
				close;
			case 2:
				mes "[Tao]";
				mes "This is no place for playing, meow..";
				mes "... Hey, it's awkward you looking at me like that, meow..";
				close;
			}
		}
		else {
			mes "[Tao]";
			mes "Is this all because of that dude called Raiyan Moore or More.. or.. blar?";
			mes "... Meow..";
			next;
			mes "[Tao]";
			mes "Ugh, you've got nothing to do, then get lost, meow?!";
			mes "You know this place is prohibited, meow.";
			mes "You won'e even be able to go into that room, anyway, meow~.";
			next;
			mes "[Tao]";
			mes "Or, else.... You could?.. Oh, who knows, meow..";
			mes "Tao hates to watch the gate anymore, meow~ meow~";
			close;
		}
	}
	else {
		mes "[Tao]";
		mes "What's up, meow!";
		next;
		switch(select("So, it's just you, little brat?:What has gotten into you?!:Hey, stop meowing?!")) {
		case 1:
			mes "[Tao]";
			mes "What are you talking about, meow!";
			mes "Tao's the greatest gate keeper!";
			mes "Don't ever low estimate, meow!";
			next;
			mes "[Tao]";
			mes "Looks like you came for some reasons, but don't even dream of getting my help, meow!!";
			mes "Do you think I'm stupid enough to tell you that";
			mes "the Master and Marjana are in that room on the right? Meow, meow, meow~~";
			close;
		case 2:
			mes "[Tao]";
			mes "Are you looking down on me because I'm a gate keeper?!";
			mes "Grrrr...";
			next;
			mes "[Tao]";
			mes "Get outta here while you have chances!!!!";
			close;
		case 3:
			mes "[Tao]";
			mes "Meow, meow.";
			mes "It's a long story, meow.";
			mes "That day, Tao really wanted a Big Ribbon, meow.";
			next;
			mes "[Tao]";
			mes "So I went out looking for Wild Roses, meow...";
			mes "...Meow? Wait a minute, why am I telling this story, meow?!";
			next;
			mes "[Tao]";
			mes "Krrrr!";
			mes "Who the hell are you!";
			next;
			mes "[Tao]";
			mes "Get out of here, meow!!!";
			close;
		}
	}
}

que_job01,51,55,0	script	que_job01#room_1	WARPNPC,1,1,{

OnReset:
	set $@maobar_room,0;
	end;

OnTouch:
	if (prt_curse == 24) {
		if ($@maobar_room == 0) {
			set $@maobar_room,1;
			donpcevent " #room1timer::OnEnable";
			donpcevent "Marjana#poison::OnEnable";
			mes "[Tao]";
			mes "Ah, that place is protected";
			mes "by security magic, so you'll";
			mes "only have ^4D4DFF4 minutes^000000 to remain";
			mes "there. Don't waste time, meow!";
			close2;
			warp "que_job01",11,7;
			end;
		}
		else {
			mes "[Tao]";
			mes "Sooo sorry, meow~";
			mes "Someone else is already";
			mes "inside. Just come back";
			mes "again later, meow?";
			close2;
			warp "que_job01",52,50;
			end;
		}
	}
	else if (mao_request > 0) {
		if ($@maobar_room == 0) {
			set $@maobar_room,1;
			donpcevent " #room1timer::OnEnable";
			donpcevent "Valdes#moc_master_1::OnEnable";
			mes "[Tao]";
			mes "Ah, that place is protected";
			mes "by security magic, so you'll";
			mes "only have ^4D4DFF4 minutes^000000 to remain";
			mes "there. Don't waste time, meow!";
			close2;
			warp "que_job01",11,7;
			end;
		}
		else {
			mes "[Tao]";
			mes "Sooo sorry, meow~";
			mes "Someone else is already";
			mes "inside. Just come back";
			mes "again later, meow?";
			close2;
			warp "que_job01",52,50;
			end;
		}
	}
	else {
		mes "[Tao]";
		mes "Wh-who are you?";
		mes "This is a restricted";
		mes "area, meow! If you don't";
		mes "have permission, then get";
		mes "out of there right meow!";
		close2;
		warp "que_job01",52,50;
		end;
	}
}

que_job01,1,1,0	script	 #room1timer	CLEAR_NPC,{
OnEnable:
	initnpctimer;
	mapannounce "que_job01","You will now enter the Master Zone, Area 1.",bc_map,"0x70DBDB";
	end;

OnStop:
	mapannounce "que_job01","The security magic in the Master Zone, Area 1 is deactivated.",bc_map,"0x70DBDB";
	donpcevent "#room1_warp::OnDisable";
	donpcevent "Valdes#moc_master_1::OnDisable";
	donpcevent "Marjana#poison::OnDisable";
	donpcevent "que_job01#room_1::OnReset";
	stopnpctimer;
	end;

OnTimer240000:
	donpcevent "#room1_warp::OnEnable";
	end;

OnTimer245000:
	donpcevent "#room1_warp::OnDisable";
	donpcevent "Valdes#moc_master_1::OnDisable";
	donpcevent "Marjana#poison::OnDisable";
	end;

OnTimer250000:
	mapannounce "que_job01","The security magic Master Zone, Area 1 is now activated.",bc_map,"0x70DBDB";
	donpcevent "que_job01#room_1::OnReset";
	stopnpctimer;
	end;
}

que_job01,12,13,0	script	#room1_warp	-1,15,15,{
OnInit:
	disablenpc "#room1_warp";
	end;

OnEnable:
	enablenpc "#room1_warp";
	end;

OnDisable:
	disablenpc "#room1_warp";
	end;

OnTouch:
	warp "que_job01",52,50;
	end;
}

que_job01,51,44,0	warp	que_job01#room_2	1,1,que_job01,80,23

que_job01,11,4,0	script	que_job01#room1_out	WARPNPC,1,1,{
OnTouch:
	warp "que_job01",52,50;
	donpcevent " #room1timer::OnStop";
	end;
}

que_job01,80,27,0	warp	que_job01#room2_1_out	1,1,que_job01,52,50

que_job01,16,21,5	script	Valdes#moc_master_1	1_M_JOBTESTER,{
	if (BaseClass == Job_Assassin) {
		mes "[Valdes]";
		mes "What is it? I have nothing to ask you to do.";
		mes "Could you please leave me alone? I have a bad headache.";
		next;
		mes "[Valdes]";
		mes "I'll ask you for a help some other time.";
		mes "You look pretty passionate after all.";
	}
	else {
		mes "[Valdez]";
		mes "....Sorry, but could you please leave?";
		mes "I'm really stressed out right now.";
		mes "If you're here for the request that we made earlier, you can just forget about it.";
	}
	if (countitem(7417) > 0) {
		delitem 7417,countitem(7417); //Mission_ScrollA
	}
	if (countitem(7418) > 0) {
		delitem 7418,countitem(7418); //Mission_ScrollB
	}
	if (countitem(7416) > 0) {
		delitem 7416,countitem(7416); //Letter_Of_Recommend
	}
	close;

OnInit:
	disablenpc "Valdes#moc_master_1";
	end;

OnEnable:
	enablenpc "Valdes#moc_master_1";
	end;

OnDisable:
	disablenpc "Valdes#moc_master_1";
	end;
}